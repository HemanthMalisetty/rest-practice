buildscript {
	repositories {
		mavenCentral()
	}
	dependencies {
		classpath("net.serenity-bdd:serenity-gradle-plugin:2.0.23")
	}
}

plugins {
	id 'org.springframework.boot' version '2.1.3.RELEASE'
	id 'java'
}

apply plugin: 'io.spring.dependency-management'
apply plugin: 'idea'
apply plugin: 'net.serenity-bdd.aggregator'
apply from: "$rootDir/gradle/libraries.gradle"

repositories {
	mavenCentral()
}

group = 'com.lisihocke'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '1.8'
targetCompatibility = '1.8'

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

defaultTasks 'clean','test','aggregate'

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	compileOnly 'org.projectlombok:lombok'
	runtimeOnly 'com.h2database:h2'
	annotationProcessor 'org.projectlombok:lombok'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	compile group: 'org.springframework.boot', name: 'spring-boot-starter-hateoas', version: '2.1.3.RELEASE'

	testCompile libs.test.serenity.core,
			libs.test.serenity.junit,
			libs.test.serenity.restAssured,
			libs.test.junit,
			libs.test.assertj
}

test {
	testLogging.showStandardStreams = true
	systemProperties System.getProperties()
}

gradle.startParameter.continueOnFailure = true

test.finalizedBy(aggregate)
